buildscript {
    ext {
        kotlin_version  = '1.1.3-2'
        fuel_version    = '1.9.0'
        mcv_version     = '0.5.0'
        dokka_version   = '0.9.15'
        bintray_version = '1.7.3'
    }

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version"
    }
}

apply plugin: 'org.jetbrains.dokka'

allprojects {
    group 'com.lapismc.lapis.launcher'
    version '0.5.0'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        maven {
            url 'http://dl.bintray.com/lapis-mc/minecraft'
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    publishing {
        publications {
            "$project.name"(MavenPublication) {
                from components.java
                groupId project.group
                artifactId "$rootProject.name-$project.name"
                version project.version
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser')   ? project.property('bintrayUser')   : System.getenv('BINTRAY_USER')
        key  = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ["$project.name"]
        pkg {
            userOrg    = 'lapis-mc'
            repo       = 'lapis'
            name       = 'launcher'
            githubRepo = 'lapis-mc/launcher'
            vcsUrl     = 'https://github.com/lapis-mc/launcher.git'
            licenses   = ['MIT']
            publish    = true
        }
    }
}